apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
  repositories.mavenInstaller {
    customizePom(pom, project)
  }
}

def customizePom(pom, gradleProject) {
  pom.whenConfigured { generatedPom ->
    // respect 'optional' and 'provided' dependencies
    gradleProject.optionalDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
    }
    gradleProject.providedDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
    }

    // eliminate test-scoped dependencies (no need in maven central poms)
    generatedPom.dependencies.removeAll { dep ->
      dep.scope == 'test'
    }

    // add all items necessary for maven central publication
    generatedPom.project {
      name = "Spring Multistore Configuration Template"
      description = "Gmind7 Bakery Spring Multistore Configuration Teamplte"
      url = 'https://github.com/gmind7/bakery-spring-multistore-config'
      organization {
        name = 'gmind7'
        url = 'http://gmind7.github.io'
      }
      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }
      scm {
        url = 'http://github.com/gmind7/bakery-spring-multistore-config'
        connection = 'scm:git:git://github.com/gmind7/bakery-spring-multistore-config'
        developerConnection = 'scm:git:git://github.com/gmind7/bakery-spring-multistore-config'
      }
      developers {
        developer {
          id = 'gmind7'
          name = 'Daesung Kim'
          email = 'gmind7@gmail.com'
        }
      }
    }
  }
}